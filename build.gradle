buildscript {
    ext {
        springBootVersion = '1.2.6.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE')
        classpath('org.springframework:springloaded:1.2.0.RELEASE')
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.tentkeep'
def versionString = '0.2.0'

repositories {
    jcenter()
    maven {
        url  "http://dl.bintray.com/jwilkey/BurrViews"
    }
}

dependencies {
    compile 'com.tentkeep:burr-views:0.2.0'
    compile 'org.springframework.boot:spring-boot-starter-web:1.3.3.RELEASE'
    compile 'com.google.guava:guava:18.0'
    compile 'org.springframework:spring-context-support:4.2.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf:1.3.3.RELEASE'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'org.springframework.boot:spring-boot-starter-test:1.3.3.RELEASE'
    compile 'com.codeborne:selenide:3.0'
    compile 'com.codeborne:phantomjsdriver:1.2.1'
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

bintray {
    user = 'jwilkey'
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'BurrTesting'
        name = 'burr-testing'
        userOrg = 'jwilkey'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jwilkey/burr-testing.git'

        labels = ['spring boot', 'thymeleaf', 'testing']
        version {
            name = versionString
            desc = 'A testing library for enabling unit test style approach for view fragments in Java Spring Boot applications'
            released  = new Date()
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.tentkeep'
            artifactId 'burr-testing'
            version versionString
            artifact sourcesJar {
                classifier 'sources'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

test {
    systemProperties = System.properties
    systemProperty "phantomjs.binary.path", phantomjsPath()
    testLogging {
        events "failed", "standardError"
    }
    afterTest { desc, result ->
        println "TESTED [${desc.className}] ${desc.name} ===${result.resultType}==="
    }
}

bootRun {
    systemProperties = System.properties
}

import org.apache.tools.ant.taskdefs.condition.Os
def isWindows() {
    return Os.isFamily(Os.FAMILY_WINDOWS)
}

def phantomjsPath() {
    return "node_modules/.bin/${(isWindows() ? "phantomjs.cmd" : "phantomjs")}"
}

allprojects {
    springBoot {
        mainClass = "workaround.gradle.application.plugin.needs.main.class"
    }
}
