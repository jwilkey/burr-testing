buildscript {
    ext {
        springBootVersion = '1.2.6.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE')
        classpath('org.springframework:springloaded:1.2.0.RELEASE')
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'

def versionString = '0.2.0'

jar {
    baseName = 'burr-testing'
    version = versionString
    archiveName "${baseName}.war"
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url  "http://dl.bintray.com/jwilkey/BurrViews"
    }
}

configurations {
    providedRuntime
}

dependencies {
    compile 'com.tentkeep:burr-views:0.2.0'
    compile 'org.springframework.boot:spring-boot-starter-web:1.3.3.RELEASE'
    compile 'com.google.guava:guava:18.0'
    compile 'org.springframework:spring-context-support:4.2.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf:1.3.3.RELEASE'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'org.springframework.boot:spring-boot-starter-test:1.3.3.RELEASE'
    compile 'com.codeborne:selenide:3.0'
    compile 'com.codeborne:phantomjsdriver:1.2.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

test {
    systemProperties = System.properties
    systemProperty "phantomjs.binary.path", phantomjsPath()
    testLogging {
        events "failed", "standardError"
    }
    afterTest { desc, result ->
        println "TESTED [${desc.className}] ${desc.name} ===${result.resultType}==="
    }
}

bootRun {
    systemProperties = System.properties
}

import org.apache.tools.ant.taskdefs.condition.Os
def isWindows() {
    return Os.isFamily(Os.FAMILY_WINDOWS)
}

def phantomjsPath() {
    return "node_modules/.bin/${(isWindows() ? "phantomjs.cmd" : "phantomjs")}"
}

allprojects {
    springBoot {
        mainClass = "workaround.gradle.application.plugin.needs.main.class"
    }
}
